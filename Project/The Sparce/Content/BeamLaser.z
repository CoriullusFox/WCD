/* ----------------------------------------------------------------------------
** TEAM - We Couldn't Decide
** © 2017 DigiPen Institute of Technology, All Rights Reserved.
**
** FILE NAME
**
** Author:
** - Brandon Wolenetz
**
** Contributors:
** - <list in same format as author if applicable>
** --------------------------------------------------------------------------*/

class BeamLaser : ZilchComponent
{
    var Cooldown : Real = 0;
    
    [Property]
    var Bullet : Archetype = Archetype.Bullet_Beam;
    
    [Property]
    var FreezTimer : Real = 0;
    
    [Property]
    var DPS : Real = 50;
    
    [Property]
    var EnergyUse : Real = 5;
    
    [Property]
    var Energy : Real = 100;
    
    [Property]
    var CooldownMax : Real = 5;
    
    [Property]
    var Player : Boolean = false;
    
    [Property]
    var CamZoom : Real = 20;
    
    function Initialize(init : CogInitializer)
    {
        this.LevelSettings.CameraViewport.Camera.Camera.Size = this.CamZoom;
        Zero.Connect(this.Owner, Events.Shoot, this.OnShoot);
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.GameSession, Events.Energy, this.OnEnergy);
    }
    
    function OnEnergy(event : Energy)
    {
        this.Energy = event.Energy;
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        //Cooldown logic
        this.Cooldown += event.Dt;
        if(this.Player)
        {
            var toSend = Cooldown();
            toSend.Cooldown = this.CooldownMax - this.Cooldown;
            toSend.MaxCooldown = this.CooldownMax;
            this.GameSession.DispatchEvent(Events.Cooldown, toSend);
        }
        
        //don't allow movement when beam is firing
        if(this.FreezTimer > 0)
        {
            this.FreezTimer -= event.Dt;
            this.Owner.RigidBody.Velocity = Real3(0,0,0);
        }
    }
    
    //Shooting logic (only runs when the event is called)
    function OnShoot(event : Shoot)
    {
        this.Player = event.Player;
        //only shoot if cooldown is over
        if(this.Cooldown >= this.CooldownMax && this.Energy >= this.EnergyUse)
        {
            if(this.Player)
            {
                //use energy
                this.Owner.EnergyStor.Energy -= this.EnergyUse;
            }
            
            
            this.Cooldown = 0;
            /** SHOOTING LOGIC HERE **/
            
            if(this.Bullet != null)
            {
                this.FreezTimer = 2;
                Console.WriteLine("bla");
                var createdBullet = this.Space.CreateAtPosition(this.Bullet, this.Owner.Transform.Translation);
                
                if(createdBullet != null)
                {
                    var randGen : Random = Random();
                    //angle of rotation
                    var angle = Math.Angle2D(event.Direction);
                    
                    //generate direction for orientation component from angle
                    var newDirection = Real3(Math.Cos(angle), Math.Sin(angle), 0);
                    
                    //apply direction and movement
                    createdBullet.Orientation.LookAtDirection(newDirection);
                    
                    createdBullet.Transform.Translation += newDirection * 0.2;
                    
                    createdBullet.Despawn.LifeLength = 2;
                    
                    //set whether or not the beam is from the player
                    createdBullet.Beam.PlayerBeam = this.Player;
                    
                    createdBullet.Beam.DPS = this.DPS;
                    
                }
            }
        }
    
    }
}
