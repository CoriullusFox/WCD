/* ----------------------------------------------------------------------------
** TEAM - We Couldn't Decide
** © 2017 DigiPen Institute of Technology, All Rights Reserved.
**
** FILE NAME
**
** Author:
** - Brandon Wolenetz
**
** Contributors:
** - <list in same format as author if applicable>
** --------------------------------------------------------------------------*/

class Shotgun : ZilchComponent
{
    var Cooldown : Real = 0.0;
    
    [Property]
    var Bullet : Archetype = Archetype.BulletTinyPellet;
    
    [Property]
    var NumberOfShots : Integer = 5;
    
    [Property]
    var DamagePerBullet : Real = 25;
    
    [Property]
    var DistBetweenShots : Real = 0.1;
    
    [Property]
    var EnergyUse : Real = 25.0;
    
    [Property]
    var Energy : Real = 100.0;
    
    [Property]
    var CooldownMax : Real = 0.6;
    
    [Property]
    var Player : Boolean = false;
    
    [Property]
    var BulletSpeed : Real = 25;
    
    [Property]
    var CamZoom : Real = 10;
    function Initialize(init : CogInitializer)
    {
        this.LevelSettings.CameraViewport.Camera.Camera.Size = this.CamZoom;
        Zero.Connect(this.Owner, Events.Shoot, this.OnShoot);
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.GameSession, Events.Energy, this.OnEnergy);
    }

    function OnEnergy(event : Energy)
    {
        this.Energy = event.Energy;
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        //Cooldown logic
        this.Cooldown += event.Dt;
        if(this.Player)
        {
            var toSend = Cooldown();
            toSend.Cooldown = this.CooldownMax - this.Cooldown;
            toSend.MaxCooldown = this.CooldownMax;
            this.GameSession.DispatchEvent(Events.Cooldown, toSend);
        }
    }
    
    //Shooting logic (only runs when the event is called)
    function OnShoot(event : Shoot)
    {
        this.Player = event.Player;
        //only shoot if cooldown is over
        if(this.Cooldown >= this.CooldownMax && this.Energy >= this.EnergyUse)
        {
            
            if(this.Player)
            {
                //use energy
                this.Owner.EnergyStor.Energy -= this.EnergyUse;
            }
            
            
            this.Cooldown = 0;
            /** SHOOTING LOGIC HERE **/
            for(var i = this.NumberOfShots; i > 0; i -= 1)
            {
                if(this.Bullet != null)
                    {
                        var createdBullet = this.Space.CreateAtPosition(this.Bullet, this.Owner.Transform.Translation);
                        
                        if(createdBullet != null)
                        {
                            var randGen : Random = Random();
                            //angle of rotation
                            var angle = (Math.Angle2D(event.Direction) + (i * this.DistBetweenShots) - (this.DistBetweenShots * this.NumberOfShots)/2) + randGen.Range(0.01, -0.01);
                            
                            //generate direction for orientation component from angle
                            var newDirection = Real3(Math.Cos(angle), Math.Sin(angle), 0);
                            
                            //apply direction and movement
                            createdBullet.Orientation.LookAtDirection(newDirection);
                            createdBullet.RigidBody.Velocity = Math.Normalize(newDirection) * 45;
                            
                            createdBullet.Despawn.LifeLength = 0.5;
                            
                            //set whether or not the bullet is from the player
                            createdBullet.BulletScript.PlayerBullet = this.Player;
                            
                            createdBullet.BulletScript.Damage = this.DamagePerBullet;
                            
                        }
                    }
            }
        }
    }
    
}
