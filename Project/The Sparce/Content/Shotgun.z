/* ----------------------------------------------------------------------------
** TEAM - We Couldn't Decide
** © 2017 DigiPen Institute of Technology, All Rights Reserved.
**
** FILE NAME
**
** Author:
** - Brandon Wolenetz
**
** Contributors:
** - <list in same format as author if applicable>
** --------------------------------------------------------------------------*/

class Shotgun : ZilchComponent
{
    var Cooldown : Real = 0.0;
    
    [Property]
    var Bullet : Archetype = Archetype.Bullet_TinyPellet;
    
    [Property]
    var NumberOfShots : Integer = 5;
    
    [Property]
    var DamagePerBullet : Real = 5;
    
    [Property]
    var DistBetweenShots : Real = 0.1;
    
    [Property]
    var EnergyUse : Real = 40;
    
    [Property]
    var Energy : Real = 100.0;
    
    [Property]
    var CooldownMax : Real = 0.8;
    
    [Property]
    var Player : Boolean = false;
    
    [Property]
    var BulletSpeed : Real = 18;
    
    [Property]
    var LifeLength : Real = 0.23;
    
    [Property]
    var CamZoom : Real = 16;
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Owner, Events.Shoot, this.OnShoot);
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.GameSession, Events.Energy, this.OnEnergy);
    }

    function OnEnergy(event : Energy)
    {
        this.Energy = event.Energy;
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        if (this.Player)
            this.LevelSettings.CameraViewport.Camera.Camera.Size = Math.Lerp(this.LevelSettings.CameraViewport.Camera.Camera.Size, this.CamZoom / 2, event.Dt * 10);
        //Cooldown logic
        this.Cooldown += event.Dt;
        if(this.Player)
        {
            var toSend = Cooldown();
            toSend.Cooldown = this.CooldownMax - this.Cooldown;
            toSend.MaxCooldown = this.CooldownMax;
            this.GameSession.DispatchEvent(Events.Cooldown, toSend);
        }
    }
    
    //Shooting logic (only runs when the event is called)
    function OnShoot(event : Shoot)
    {
        this.Player = event.Player;
        //only shoot if cooldown is over
        if(this.Cooldown >= this.CooldownMax && this.Energy >= this.EnergyUse)
        {
            this.Space.SoundSpace.PlayCue(SoundCue.Find("Gun_EchoLaser"));
            
            this.Owner.DispatchEvent(Events.Fire, Fire());
            if(this.Player)
            {
                //use energy
                this.Owner.EnergyStor.Energy -= this.EnergyUse;
            }
            
            
            this.Cooldown = 0;
            /** SHOOTING LOGIC HERE **/
            for(var i = this.NumberOfShots; i > 0; i -= 1)
            {
                if(this.Bullet != null)
                    {
                        
                        //kickback
                        this.Owner.Transform.Translation -= event.Direction * 0.02;
                        
                        var createdBullet = this.Space.CreateAtPosition(this.Bullet, this.Owner.Transform.Translation);
                        if(this.Player)
                        {
                            
                            var toSend = ScreenShake();
                            toSend.Intencity = 0.25;
                            toSend.Timer = 0.1;
                            toSend.FadeSpeed = 0.1;
                            this.Space.DispatchEvent(Events.ScreenShake, toSend);
                            
                        }
                        if(createdBullet != null)
                        {
                            var randGen : Random = Random();
                            //angle of rotation
                            var angle = (Math.Angle2D(event.Direction) + (i * this.DistBetweenShots) - (this.DistBetweenShots * this.NumberOfShots)/2) + randGen.Range(0.01, -0.01);
                            
                            //generate direction for orientation component from angle
                            var newDirection = Real3(Math.Cos(angle), Math.Sin(angle), 0);
                            
                            //apply direction and movement
                            createdBullet.Orientation.LookAtDirection(newDirection);
                            createdBullet.Transform.Translation += newDirection;
                            createdBullet.RigidBody.Velocity = Math.Normalize(newDirection) * this.BulletSpeed;
                            
                            createdBullet.Despawn.LifeLength = this.LifeLength;
                            
                            //set whether or not the bullet is from the player
                            createdBullet.BulletScript.PlayerBullet = this.Player;
                            
                            createdBullet.BulletScript.Damage = this.DamagePerBullet;
                            
                        }
                    }
            }
        }
    }
    
}
