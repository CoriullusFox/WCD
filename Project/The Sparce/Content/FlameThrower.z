/* ----------------------------------------------------------------------------
** TEAM - We Couldn't Decide
** © 2017 DigiPen Institute of Technology, All Rights Reserved.
**
** FILE NAME
**
** Author:
** - Brandon Wolenetz
**
** Contributors:
** - <list in same format as author if applicable>
** --------------------------------------------------------------------------*/

class FlameThrower : ZilchComponent
{
    var Cooldown : Real = 0;
    
    [Property]
    var Bullet : Archetype = Archetype.Bullet_FlamethrowerAmmo;
    
    [Property]
    var DPS : Real = 50;
    
    [Property]
    var EnergyUse : Real = 75;
    
    [Property]
    var Energy : Real = 100;
    
    [Property]
    var CooldownMax : Real = 2.5;
    
    [Property]
    var Player : Boolean = false;
    
    [Property]
    var CamZoom : Real = 10;
    
    function Initialize(init : CogInitializer) 
    {
        this.LevelSettings.CameraViewport.Camera.Camera.Size = this.CamZoom;
        Zero.Connect(this.Owner, Events.Shoot, this.OnShoot);
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.GameSession, Events.Energy, this.OnEnergy);
    }
    
    function OnEnergy(event : Energy)
    {
        this.Energy = event.Energy;
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        //Cooldown logic
        this.Cooldown += event.Dt;
        if(this.Player)
        {
            var toSend = Cooldown();
            toSend.Cooldown = this.CooldownMax - this.Cooldown;
            toSend.MaxCooldown = this.CooldownMax;
            this.GameSession.DispatchEvent(Events.Cooldown, toSend);
        }
    }
    
    //Shooting logic (only runs when the event is called)
    function OnShoot(event : Shoot)
    {
        this.Player = event.Player;
        //only shoot if cooldown is over
        if(this.Cooldown >= this.CooldownMax && this.Energy >= this.EnergyUse)
        {
            
            
            if(this.Player)
            {
                //use energy
                this.Owner.EnergyStor.Energy -= this.EnergyUse;
            }
            
            
            this.Cooldown = 0;
            /** SHOOTING LOGIC HERE **/
            
            if(this.Bullet != null)
            {
                var createdBullet = this.Space.CreateAtPosition(this.Bullet, Real3(0,0,0));
                
                if(createdBullet != null)
                {
                    //make the flame a child of the owner
                    createdBullet.AttachTo(this.Owner);
                    
                    createdBullet.Transform.Translation = Real3(0,0,0);
                    
                    var randGen : Random = Random();
                    //angle of rotation
                    var angle = Math.Angle2D(event.Direction) + randGen.Range(0.1, 0.1*-1);
                    
                    //generate direction for orientation component from angle
                    var newDirection = Real3(Math.Cos(angle), Math.Sin(angle), 0);
                    
                    //apply direction and movement
                    createdBullet.Orientation.LookAtDirection(newDirection);
                    
                    createdBullet.Despawn.LifeLength = 5;
                    
                    //tell the flame that it is whether it's from the player
                    if(createdBullet.Flame != null)
                    {
                        createdBullet.Flame.PlayerFlame = this.Player;
                    }
                    
                    createdBullet.Flame.DPS = this.DPS;
                    
                }
            }
        }
    }
}
