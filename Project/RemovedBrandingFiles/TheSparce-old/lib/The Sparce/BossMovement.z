class BossMovement : ZilchComponent
{
    
    [Property]
    var ResponceTime : Real = 2;
    
    [Property]
    var Pathfind : Boolean = true;
    
    [Property]
    var Debug : Boolean = false;
    
    [Property]
    var MoveDirection : Real3 = Real3(0.0, 0.0, 0.0);
    
    [Property]
    var Speed : Integer = 100;
    
    var Timer : Real;
    
    var PathFindTimer : Real = 0.25;
    
    var Path : Array[Node] = Array[Node]();
    
    var FreezeTimer : Real = 0;
    
    var Frozen : Boolean = false;
    
    function Freeze(time : Real)
    {
        
        this.FreezeTimer = time;
        this.Frozen = true;
        
    }
    
    function Initialize(init : CogInitializer)
    {
        this.Timer = this.ResponceTime;
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }
    
    function OnLogicUpdate(event : UpdateEvent)
    {
        
        if(this.Frozen)
        {
            this.Owner.RigidBody.Velocity = Real3(0,0,0);
            this.FreezeTimer -= event.Dt;
            if(this.FreezeTimer <= 0)
            {
                this.Frozen = false;
            }
        }
        
        this.PathFindTimer -= event.Dt;
        this.Timer -= event.Dt;
        var player = this.Space.FindObjectByName("TempPlayer");
        if (player == null) return;
        var distanceFromPlayer = Math.Distance(this.Owner.Transform.Translation, player.Transform.Translation);
        
        if(this.Pathfind && this.Path != null && this.Path.Count > 1)
        {
            
            
            var temp = this.Path[0].WorldPos;
            //draw debug lines
            if(this.Debug || this.LevelSettings.DebugMode.DubugModeEnabled)
                {
                for (var i = 1; i < this.Path.Count; ++i)
                {
                    var debugLine = DebugLine();
                    debugLine.Start(temp);
                    debugLine.End(this.Path[i].WorldPos);
                    debugLine.Float4Color(Real4(0,0,1,1));
                    debugLine.Width(3);
                    temp = this.Path[i].WorldPos;
                    debugLine.Draw();
                    
                }
            }
            
            //follow path
            if(Math.Distance(this.Path[0].WorldPos, this.Owner.Transform.Translation) > 0.3)
            {
                this.MoveDirection = this.Path[0].WorldPos - this.Owner.Transform.Translation;
            }
            else
            {
                if(this.PathFindTimer <= 0 && this.Pathfind)
                {
                    this.PathFindTimer = 0.25;
                    this.Timer = this.ResponceTime;
                    var myPos = this.Owner.Transform.Translation;// - Real3(0, this.Owner.Transform.Scale.Y / 2, 0);
                    var playerPos = player.Transform.Translation;// - Real3(0, player.Transform.Scale.Y / 2, 0);
                    
                    this.Path = this.Space.FindObjectByName("PlayerSpawnPoint").AStarPathfind.PathFind(myPos, playerPos);
                }
                if(this.Pathfind && this.Path != null && this.Path.Count > 0)
                {
                    this.Path.RemoveAt(0);
                }
                if(this.Path != null && this.Path.Count > 0) 
                    this.MoveDirection = this.Path[0].WorldPos - this.Owner.Transform.Translation;
            }
            
            this.Move(this.MoveDirection);
            
        }
        
        if((this.Path != null && this.Path.Count <= 1 || this.Timer <= 0) && this.PathFindTimer <= 0 && this.Pathfind && this.Path != null)
        {
            this.PathFindTimer = 0.25;
            this.Timer = this.ResponceTime;
            this.Path = this.Space.FindObjectByName("PlayerSpawnPoint").AStarPathfind.PathFind(this.Owner.Transform.Translation, this.Space.FindObjectByName("TempPlayer").Transform.Translation);
        }
        
        if(!this.Pathfind || this.Path == null)
        {
            
            this.MoveDirection = this.Space.FindObjectByName("TempPlayer").Transform.Translation - this.Owner.Transform.Translation;
            this.Move(this.MoveDirection);
        }
        
    }
    
    function Move(dir : Real3)
    {
        
        this.Owner.RigidBody.Velocity = Math.Normalize(dir) * this.Space.TimeSpace.Dt * this.Speed;
        
    }
}
